$PROB MM_BSH
$INPUT ID TIME DV WT AMT CMT MDV DOSE 
$DATA nonlinear.csv IGNORE=@

$SUBR ADVAN6 TOL=6

$MODEL
NCOMP=1 
COMP=(CENTRAL,DEFDOSE, DEFOBS) 

$PK

 TVVM = THETA(1)	 
 TVKM = THETA(2) 
 TVV  = THETA(3)


 VM  = TVVM*EXP(ETA(1))
 KM  = TVKM*EXP(ETA(2))
 V   = TVV*EXP(ETA(3))
 S1 = V

$DES	
 C1 = A(1)/S1    
 DADT(1) = -VM*C1/(KM+C1) 

$ERROR
IPRED = F
IRES  = DV - IPRED
W = SQRT(THETA(4)**2 + THETA(5)**2 * IPRED**2)
IWRES = IRES/W
Y     = F + W*EPS(1)

$THETA 	
(0, 30)	
(0, 10) 
(0, 6) 
0.001 FIX
0.1

$OMEGA 
 0 FIX ; ETAVM	
 0.1   ; 	ETAKM	
 0.1   ;	ETAV 	
 
$SIGMA
 1 FIX 

$EST METHOD=1 INT MAXEVAL=9999 PRINT=5 NSIG=3 NOABORT
$COV PRINT=E

$TABLE ID TIME DV WT AMT CMT MDV IPRED IWRES CWRES NOPRINT ONEHEADER FILE=sdtab1002
$TABLE ID VM KM V ETA1 ETA2 ETA3 NOAPPEND NOPRINT ONEHEADER FILE=patab1002
$TABLE ID DOSE ETA1 ETA2 ETA3 NOAPPEND NOPRINT ONEHEADER FILE=cotab1002



